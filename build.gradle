plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'io.freefair.lombok' version '5.3.0'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'war'
}

group = 'com.marcdenning.azure'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('azureVersion', '2.3.5')
    set('azureAppInsightsVersion', '2.6.2')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "com.microsoft.azure:applicationinsights-spring-boot-starter:${azureAppInsightsVersion}"
    implementation "com.microsoft.azure:applicationinsights-logging-logback:${azureAppInsightsVersion}"
    implementation 'com.microsoft.azure:azure-spring-boot-metrics-starter'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

dependencyManagement {
    imports {
        mavenBom "com.microsoft.azure:azure-spring-boot-bom:${azureVersion}"
    }
}

bootWar {
    archiveFileName = 'ROOT.war'
}

checkstyleMain {
    configFile = file("checkstyle.xml")
}

checkstyleTest {
    configFile = file("checkstyle.xml")
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.6"
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }
}
